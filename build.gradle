plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.4.32'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding 'UTF-8'

group 'me.dkim19375'
version '2.6.0-SNAPSHOT'

//noinspection GrUnresolvedAccess
compileKotlin.kotlinOptions {
    languageVersion = "1.5"
    jvmTarget = '1.8'
}

license {
    header = rootProject.file("LICENSE")
    include "**/*.kt"
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://repo.mattstudios.me/artifactory/public' }
}

dependencies {
    compileOnly 'net.dv8tion:JDA:4.2.1_262'
    api 'commons-io:commons-io:2.8.0'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'com.github.minndevelopment:jda-ktx:0.4.0'
    api 'me.mattstudios:triumph-config:1.0.5-SNAPSHOT'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    from dokkaJavadoc
    classifier 'javadoc'
}

publishing {
    repositories {
/*        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = releasesRepoUrl

            credentials {
                it.username mavenUsername
                it.password mavenPassword
            }
        }*/
    }

    def project = project
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.github.dkim19375'
            artifactId 'dkim19375jdautils'
            version project.version

            from components.kotlin
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'dkim19375JDAUtils'
                description = 'A kotlin library used to help JDA developers make bots either!'
                url = 'https://github.com/dkim19375/dkim19375JDAUtils'

                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'dkim19375'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/dkim19375/dkim19375JDAUtils.git'
                    developerConnection = 'scm:git:ssh://github.com:dkim19375/dkim19375JDAUtils.git'
                    url = 'https://github.com/dkim19375/dkim19375JDAUtils'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = mavenUsername
            password = mavenPassword
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.jar.dependsOn licenseFormat