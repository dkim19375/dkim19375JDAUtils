import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.9.10'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

final KOTLIN_VER = '1.9.22'

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding 'UTF-8'

group 'me.dkim19375'
version '2.10.10'

//noinspection GrUnresolvedAccess
compileKotlin.compilerOptions {
    jvmTarget.set(JvmTarget.JVM_1_8)
}

compileTestKotlin.compilerOptions {
    jvmTarget.set(JvmTarget.JVM_1_8)
}

license {
    header = rootProject.file 'LICENSE'
    include '**/*.kt'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://repo.triumphteam.dev/snapshots/' }
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/releases/' }
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'org.openjdk.nashorn:nashorn-core:15.4'

    //noinspection GradlePackageUpdate
    api 'commons-io:commons-io:2.15.1'
    api 'net.dv8tion:JDA:5.0.0-beta.19'
    api 'org.apache.commons:commons-lang3:3.14.0'
    api 'com.github.minndevelopment:jda-ktx:0.10.0-beta.1'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VER"
    //noinspection GradlePackageUpdate
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    //noinspection GradlePackageUpdate
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.7.3'
    api 'io.github.dkim19375:dkimcore:1.5.0'
    // api 'org.codehaus.groovy:groovy-jsr223:3.0.8'

    // testImplementation "net.dv8tion:JDA:$JDA_VER"
    testImplementation 'net.dv8tion:JDA:5.0.0-beta.19'
    testImplementation 'commons-io:commons-io:2.15.1'
    testImplementation 'org.apache.commons:commons-lang3:3.14.0'
    testImplementation 'com.github.minndevelopment:jda-ktx:0.10.0-beta.1'

    // testing libs
    testImplementation "org.jetbrains.kotlin:kotlin-test:$KOTLIN_VER"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
}

tasks.register('dokkaHtmlJar', Jar.class) {
    dependsOn(dokkaHtml)
    from(dokkaHtml)
    archiveClassifier.set("javadoc")
}

publishing {
    repositories {
/*        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = releasesRepoUrl

            credentials {
                it.username mavenUsername
                it.password mavenPassword
            }
        }*/
    }

    def project = project
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            groupId 'io.github.dkim19375'
            //noinspection GroovyAssignabilityCheck
            artifactId 'dkim19375jdautils'
            version project.version

            //noinspection GroovyAssignabilityCheck
            from components.kotlin
            //noinspection GroovyAssignabilityCheck
            artifact kotlinSourcesJar
            //noinspection GroovyAssignabilityCheck
            artifact dokkaHtmlJar

            pom {
                //noinspection GroovyAssignabilityCheck
                name = 'dkim19375JDAUtils'
                //noinspection GroovyAssignabilityCheck
                description = 'A kotlin library used to help JDA developers make bots either!'
                //noinspection GroovyAssignabilityCheck
                url = 'https://github.com/dkim19375/dkim19375JDAUtils'

                //noinspection GroovyAssignabilityCheck
                packaging = 'jar'

                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name = 'MIT License'
                        //noinspection GroovyAssignabilityCheck
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        //noinspection GroovyAssignabilityCheck
                        id = 'dkim19375'
                    }
                }

                scm {
                    //noinspection GroovyAssignabilityCheck
                    connection = 'scm:git:git://github.com/dkim19375/dkim19375JDAUtils.git'
                    //noinspection GroovyAssignabilityCheck
                    developerConnection = 'scm:git:ssh://github.com:dkim19375/dkim19375JDAUtils.git'
                    //noinspection GroovyAssignabilityCheck
                    url = 'https://github.com/dkim19375/dkim19375JDAUtils'
                }
            }
        }
    }
}

nexusPublishing {
    packageGroup.set 'io.github.dkim19375'
    repositories {
        sonatype {
            nexusUrl = uri 'https://s01.oss.sonatype.org/service/local/'
            snapshotRepositoryUrl = uri 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            username = project.findProperty 'mavenUsername'
            password = project.findProperty 'mavenPassword'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.jar.dependsOn licenseFormat